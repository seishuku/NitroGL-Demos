!!ARBfp1.0

# Blinn lighting via I=Ka+Base*(Kd*Falloff*(N.L))+(Ks*Gloss*Self*(N.H)^n)

# Input attributes

# Texture coords
ATTRIB Texture=fragment.texcoord[0];

# Vertex coords
ATTRIB Vertex=fragment.texcoord[1];

# Tangent basis matrix (transpose)
ATTRIB TangentX=fragment.texcoord[2];
ATTRIB TangentY=fragment.texcoord[3];
ATTRIB TangentZ=fragment.texcoord[4];

# Output
OUTPUT Color=result.color;

# Light parameters
PARAM Light=program.env[0];
PARAM Kd=program.env[1];
PARAM Ks=program.env[2];
PARAM Ka=program.env[3];

PARAM mvinvtrans[4]={ state.matrix.modelview.invtrans };
PARAM Eye={ 0.0, 0.0, 0.0, 1.0 };

PARAM Bias=program.local[0];

TEMP Base, N, L, H, V, NdotL, NdotH, Falloff;

TEX Base, Texture, texture[0], 2D;
TEX N, Texture, texture[1], 2D;

# Scale and bias normal to get signed
MAD L, N, 2.0, -1.0;

MOV L.z, 0.0;
DP3 L.z, L, L;
SUB L.z, 1.0, L.z;
RSQ L.z, L.z;
RCP L.z, L.z;

# Transform it into object space
DP3 N.x, TangentX, L;
DP3 N.y, TangentY, L;
DP3 N.z, TangentZ, L;

# Normalize it
DP3 N.w, N, N;
RSQ N.w, N.w;
MUL N, N, N.w;

# Light to vertex direction
SUB L, Light, Vertex;

# D = L*(1/Radius)
# Falloff = 1-D^2
MUL Falloff, L, Light.w;
DP3 Falloff, Falloff, Falloff;
SUB_SAT Falloff, 1.0, Falloff;

# Normalize it
DP3 L.w, L, L;
RSQ L.w, L.w;
MUL L, L, L.w;

# Get view direction
DP4 V.x, mvinvtrans[0], Eye;
DP4 V.y, mvinvtrans[1], Eye;
DP4 V.z, mvinvtrans[2], Eye;
DP4 V.w, mvinvtrans[3], Eye;

# View to vertex direction and normalize it
SUB V, V, Vertex;
DP3 V.w, V, V;
RSQ V.w, V.w;
MUL V, V, V.w;

# H = (L+V)/2
ADD H, L, V;
MUL H, H, 0.5;

# Normalize it
DP3 H.w, H, H;
RSQ H.w, H.w;
MUL H, H, H.w;

# Specular = Ks*Gloss*(N.H)^n
DP3 NdotH, N, H;
MAX NdotH, 0.0, NdotH;
POW NdotH, NdotH.x, Ks.w;
MUL NdotH, Base.a, NdotH;
MUL NdotH, Ks, NdotH;

# Diffuse = Ka+Kd*Falloff*(N.L)
DP3 NdotL, N, L;
MAX NdotL, 0.0, NdotL;
MUL NdotL, Falloff, NdotL;
MUL NdotH, NdotL, NdotH;
MUL NdotL, Kd, NdotL;

ADD Base, Ka, Base;

# I=Ka+Base*(Kd*Falloff*(N.L))+(Ks*Gloss*Self*(N.H)^n)
MAD Color.rgb, Base, NdotL, NdotH;

END
