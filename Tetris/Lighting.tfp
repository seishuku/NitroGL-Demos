!!ARBfp1.0

ATTRIB Color=fragment.color;
ATTRIB NormalU=fragment.texcoord[0];
ATTRIB ViewU=fragment.texcoord[1];

OUTPUT Output=result.color;

PARAM Matrix[12]={ program.env[0..11] };

TEMP Normal, View, Reflect, Diffuse, Temp;

// Renormalize
DP3 Temp.x, NormalU, NormalU;
RSQ Temp.w, Temp.x;
MUL Normal, NormalU, Temp.w;
MOV Normal.w, 1.0;

// View direction
DP3 Temp.x, ViewU, ViewU;
RSQ Temp.w, Temp.x;
MUL View, ViewU, Temp.w;

// Reflection
DP3 Temp.x, View, Normal;
ADD Temp.w, Temp.x, Temp.x;
MAD Reflect, Temp.w, Normal, -View;

TEX Reflect, Reflect, texture[1], CUBE;

DP4 Temp.x, Matrix[ 0], Normal;
DP4 Temp.y, Matrix[ 1], Normal;
DP4 Temp.z, Matrix[ 2], Normal;
DP4 Temp.w, Matrix[ 3], Normal;
DP4 Diffuse.x, Normal, Temp;
DP4 Temp.x, Matrix[ 4], Normal;
DP4 Temp.y, Matrix[ 5], Normal;
DP4 Temp.z, Matrix[ 6], Normal;
DP4 Temp.w, Matrix[ 7], Normal;
DP4 Diffuse.y, Normal, Temp;
DP4 Temp.x, Matrix[ 8], Normal;
DP4 Temp.y, Matrix[ 9], Normal;
DP4 Temp.z, Matrix[10], Normal;
DP4 Temp.w, Matrix[11], Normal;
DP4 Diffuse.z, Normal, Temp;

MAD Output, Color, Diffuse, Reflect;

END
